/**
 * Message Encoding Utility Functions
 *
 * @design For verification purposes, we decided to return
 * Option types as the result of the `Encode` and `Decode`
 * functions defined in the Algebra::EncoderI interface.
 * For usability, this is not optimal since it forces us
 * to unpack those option values in several places and deal
 * with the None case when we assume such ought be avoided
 * by design. Hence, this module defines so-called "strict"
 * versions of those functions with stronger preconditions;
 * these version raise errors if passed arguments for which
 * the original functions would return None.
 *
 * @author Frank Zeyda (frank.zeyda@freeandfair.us)
 * @copyright Free & Fair 2025
 * @version 0.1
 */
module Algebra::EncoderUtils where

import interface Algebra::EncoderI

/**
 * Encodes a message into a group element. (strict version)
 *
 * This function raises an error iff the message is not
 * encodable, i.e., m âˆ‰ encodable. An advantage compared
 * to `Encode` is that the result is not wrapped into an
 * `Option T` type.
 */
Strict'Encode : M -> T
Strict'Encode x =
  case (Encode x) of
    Some y -> y
    None   -> error "Strict'Encode requires message to be encodable"

/**
 * Decodes a group element into a message. (strict version)
 *
 * This function shall return None iff the group element
 * does not result from applying Encode to an encodable
 * message.
 */
Strict'Decode : T -> M
Strict'Decode y =
  case (Decode y) of
    Some x -> x
    None   -> error "Strict'Decode requires message to be decodable"

/**
 * Encodes a list of messages into a list of group element.
 *
 * @note This function is useful when dealing with product
 * groups.
 */
EncodeList : {n} (fin n) => [n]M -> [n](Option T)
EncodeList = map Encode

/**
 * Decodes a list of group element into a list of message.
 *
 * @note This function is useful when dealing with product
 * groups.
 */
DecodeList : {n} (fin n) => [n]T -> [n](Option M)
DecodeList = map Decode

/**
 * Encodes a list of messages into a list of group element.
 *
 * @note This function is useful when dealing with product
 * groups.
 */
Strict'EncodeList : {n} (fin n) => [n]M -> [n]T
Strict'EncodeList = map Strict'Encode

/**
 * Decodes a list of group element into a list of message.
 *
 * @note This function is useful when dealing with product
 * groups.
 */
Strict'DecodeList : {n} (fin n) => [n]T -> [n]M
Strict'DecodeList = map Strict'Decode
